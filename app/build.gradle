apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'jacoco.gradle'

android {
  compileSdkVersion 29

  defaultConfig {
    applicationId 'com.github.ashutoshgngwr.noice'
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 25
    versionName '0.7.2'
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  signingConfigs {
    release {
      storeFile file('../keystore.jks')
      keyAlias "$System.env.KEY_ALIAS"
      storePassword "$System.env.STORE_PASSWORD"
      keyPassword "$System.env.KEY_PASSWORD"
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      testCoverageEnabled true
    }

    release {
      shrinkResources true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig = signingConfigs.release
    }
  }

  flavorDimensions 'distribution_channel'
  productFlavors {
    fdroid {
      dimension 'distribution_channel'
      buildConfigField 'boolean', 'IS_PLAY_STORE_BUILD', 'false'
    }

    playstore {
      dimension 'distribution_channel'
      buildConfigField 'boolean', 'IS_PLAY_STORE_BUILD', 'true'
    }
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = 1.8
  }

  testOptions {
    execution 'ANDROIDX_TEST_ORCHESTRATOR'
    unitTests {
      includeAndroidResources = true
    }
  }
}

dependencies {
  implementation 'androidx.core:core-ktx:1.3.0'
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.media:media:1.1.0'
  implementation 'androidx.mediarouter:mediarouter:1.1.0'
  implementation 'androidx.preference:preference:1.1.1'
  implementation 'com.github.medyo:android-about-page:1.2.5'
  implementation 'com.google.android.exoplayer:exoplayer-core:2.11.5'
  implementation 'com.google.android.material:material:1.1.0'
  implementation 'com.google.code.gson:gson:2.8.6'
  implementation 'org.greenrobot:eventbus:3.1.1'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  playstoreImplementation 'com.google.android.gms:play-services-cast-framework:18.1.0'

  testImplementation 'androidx.test:core:1.2.0'
  testImplementation 'io.mockk:mockk:1.10.0'
  testImplementation 'junit:junit:4.13'
  testImplementation 'org.robolectric:robolectric:4.3.1'
  testImplementation 'org.skyscreamer:jsonassert:1.5.0'

  androidTestImplementation 'androidx.test:core:1.2.0'
  androidTestImplementation 'androidx.test:rules:1.2.0'
  androidTestImplementation 'androidx.test:runner:1.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'io.mockk:mockk-android:1.10.0'
  androidTestImplementation 'junit:junit:4.13'

  androidTestUtil 'androidx.test:orchestrator:1.2.0'

  debugImplementation 'androidx.fragment:fragment-testing:1.2.5'
}
